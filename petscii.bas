10 GOTO 320
20 REM MODULO
30 MOD%=INT((X/Y-INT(X/Y))*Y+.5):RETURN
35 REM PRINT HEADING AND CHARACTER
40 X=I:Y=16:GOSUB 30
50 IF MOD%<>0 THEN GOTO 80
60 IF I<>$20 THEN PRINT:PRINT "  ";V$;
70 PRINT:PRINT CHR$(I/16+N);"0";V$;
80 PRINT CHR$(I);" ";
90 RETURN
95 REM DELAY
100 T1=TIME
110 D=60*D/1000
120 IF TIME-T1<D THEN GOTO 120
130 RETURN
135 REM WAIT 1 S AND CURSOR HOME
140 D=1000:GOSUB 100
150 PRINT CHR$(19)
160 RETURN
165 REM PRINT TABLE
170 N=ASC("0")
180 B$=CHR$($60):V$=CHR$($7D)
190 PRINT:PRINT:PRINT:PRINT "  ";V$;
200 FOR I=ASC("0") TO ASC("9"):PRINT CHR$(I);" ";:NEXT
210 FOR I=ASC("A") TO ASC("F"):PRINT CHR$(I);" ";:NEXT
220 PRINT:PRINT B$;B$;CHR$($7B);:FOR I=1 TO 31:PRINT B$;:NEXT
230 FOR I=$20 TO $7F
240 GOSUB 40
250 NEXT
260 N=ASC("7")
270 FOR I=$A0 TO $FF
280 GOSUB 40
290 NEXT
300 PRINT CHR$(19)
310 RETURN
315 REM MAIN LOOP
320 IF PEEK($D9)<>40 THEN SYS$FF5F:PRINT
330 GOSUB 170
340 PRINT CHR$(14):PRINT "PETSCII (SHIFTED)  "
350 GOSUB 140
360 PRINT CHR$(142):PRINT "PETSCII (UNSHIFTED)"
370 GOSUB 140
380 GOTO 340
RUN
 
