10 GOTO 1010
100 POKE $7E,L:POKE $7F,H
110 POKE 780,$7E:POKE 781,B:POKE 782,0:SYS $FF74:REM FETCH
120 G=PEEK(780):RETURN
1010 SCREEN $FF:COLOR 14,0:CLS:I=1
1014 B=4:REM MAY CHANGE FOR NEW ROM
1020 BASE=$C0DE-18:REM -"-
1024 DIM K$(107+16),A$(107+16):REM -"-
1030 FOR P=BASE TO BASE+388+128:REM -"-
1033 H=INT(P/256):L=P-256*H
1040 GOSUB 100
1044 IF (G AND 127)<32 GOTO 1070
1050 W$=W$+CHR$(G AND 127)
1060 IF G>127 THEN GOSUB 1410
1070 NEXT
1080 M=I-1
1090 COLOR 7:PRINT CHR$(14)
1100 FOR J=1 TO M
1110 N=2
1120 IF LEN(K$(J))<=N THEN A$(J)=K$(J):GOTO 1166
1130 A$(J)=LEFT$(K$(J),N)
1134 IF J=1 GOTO 1166
1140 FOR L=1 TO J-1
1150 IF LEFT$(K$(L),N)=A$(J) THEN N=N+1:A$(J)=LEFT$(K$(J),N):GOTO 1140
1160 NEXT
1166 IF LEN(K$(J))=LEN(A$(J)) THEN GOSUB 1310:GOTO 1180
1170 A$(J)=LEFT$(A$(J),LEN(A$(J))-1)+CHR$(ASC(MID$(A$(J),LEN(A$(J)),1))+32)
1180 PRINT MID$(STR$(10*J),2);" ";A$(J);":REM ";K$(J)
1190 NEXT
1200 CLS:COLOR 15:PRINT "cOMMANDER x16 basic V2 aBBREVIATIONS:":COLOR 13
1204 FOR J=1 TO M:IF LEN(K$(J))=1 THEN NEXT
1206 PRINT A$(J);"=";K$(J),:IF K$(J)="GO" THEN COLOR 1
1208 NEXT
1210 GOTO 1210
1220 END
1310 IF A$(J)="PRINT" THEN A$(J)="?"
1320 RETURN
1410 K$(I)=W$:PRINT W$:W$="":IF I=1 AND K$(I)<>"END" THEN RETURN
1420 IF LEFT$(K$(I),4)="TOO " GOTO 1080:REM POP
1430 I=I+1:RETURN
